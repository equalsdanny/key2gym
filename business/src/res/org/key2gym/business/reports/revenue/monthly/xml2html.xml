<?xml version="1.0" encoding="utf-8"?>
<!--
   Copyright 2012 Danylo Vashchilenko

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
-->

<xsl:stylesheet version="2.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
  
  <xsl:output method="html" encoding="utf-8" indent="yes"/>
 
  <!-- I18n strings. -->
  <xsl:param name="Label.Title" />
  <xsl:param name="Label.IntervalStart" />
  <xsl:param name="Label.IntervalEnd" />
  <xsl:param name="Label.Generated" />
  <xsl:param name="Text.MonthNumber" />
  <xsl:param name="Text.Month" />
  <xsl:param name="Text.Revenue" />

  <xsl:template match="/">    
    <html>
      <head>
	<meta http-equiv="Content-Type" content="text/html; charset=UTF-8" />
	<style type="text/css">
	  <xsl:value-of select="unparsed-text('file:///style.css', 'utf-8')" />
	</style>

	<script type="text/javascript">
	  <xsl:value-of select="unparsed-text('file:///flotr2.js', 'utf-8')" />
	</script>
      </head>
      <body>
	<xsl:apply-templates select="monthlyRevenueReport"/>
      </body>
    </html>
  </xsl:template>

  <xsl:template match="monthlyRevenueReport">
    <table>
      <tr>
	<td class="header"><xsl:value-of select="$Label.Title" /></td>
	<td><xsl:value-of select="title" /></td>
      </tr>
      <tr>
	<td class="header"><xsl:value-of select="$Label.IntervalStart" /></td>
	<td><xsl:value-of select="format-date(intervalStart, '[Y0001]-[M01]')" /></td>
      </tr>
      <tr>
	<td class="header"><xsl:value-of select="$Label.IntervalEnd" /></td>
	<td><xsl:value-of select="format-date(intervalEnd, '[Y0001]-[M01]')" /></td>
      </tr>
      <tr>
	<td class="header"><xsl:value-of select="$Label.Generated" /></td>
	<td><xsl:value-of select="format-dateTime(generated, '[Y0001]-[M01]-[D01] [H01]:[m01]:[s01]')" /></td>
      </tr>
    </table>
    <div id="graph"></div>
    <script type="text/javascript">
      (function basic(container) {
      
      var data = [
      <xsl:for-each select="month">
	[<xsl:value-of select="number"/>, <xsl:value-of select="revenue" />],
      </xsl:for-each>
      ]

      var graph = Flotr.draw(container, [data],
        {
	  fontSize: 13,
	  parseFloat: false,
	  bars : {
	    lineWidth: 1,
	    show: true,
	    horizontal: false,
	    barWidth: 1.0
	  },
	  yaxis: {
	    min: 0,
	    autoScale: true
	  },
	  xaxis: {
	    autoscale: true,
	    min: 1,
	    noTicks: <xsl:value-of select="count(month)" />,
	    tickDecimals: 0
          },
	  mouse: {
	    track: true,
	    trackDecimals: 2,
	    relative: true
	  }
      });

      })(document.getElementById("graph"));
    </script>
    <table>
      <tr>
	<td class="header"><xsl:value-of select="$Text.MonthNumber" /></td>
	<td class="header"><xsl:value-of select="$Text.Month" /></td>
	<td class="header"><xsl:value-of select="$Text.Revenue" /></td>
      </tr>
      <xsl:apply-templates select="month" />
    </table>
  </xsl:template>
  <xsl:template match="month">
    <tr>
      <td><xsl:value-of select="number" /></td>
      <td><xsl:value-of select="format-date(month, '[Y0001]-[M01]')" /></td>
      <td><xsl:value-of select="revenue" /></td>
    </tr>
  </xsl:template>
</xsl:stylesheet>
