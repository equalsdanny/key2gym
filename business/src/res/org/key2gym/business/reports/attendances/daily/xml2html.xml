<?xml version="1.0" encoding="utf-8"?>
<xsl:stylesheet version="2.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
  
  <xsl:output method="html" encoding="utf-8" indent="yes"/>

  <!-- I18n strings. -->
  <xsl:param name="Label.Title" />
  <xsl:param name="Label.RangeBegin" />
  <xsl:param name="Label.RangeEnd" />
  <xsl:param name="Label.Generated" />
  <xsl:param name="Text.DayNumber" />
  <xsl:param name="Text.Date" />
  <xsl:param name="Text.Attendances" />

  <xsl:template match="/">    
    <html>
      <head>
	<meta http-equiv="Content-Type" content="text/html; charset=UTF-8" />
	<style type="text/css">
	  <xsl:value-of select="unparsed-text('file:///style.css', 'utf-8')" />
	</style>

	<script type="text/javascript">
	  <xsl:value-of select="unparsed-text('file:///flotr2.js', 'utf-8')" />
	</script>
      </head>
      <body>
	<xsl:apply-templates select="dailyAttendancesReport"/>
      </body>
    </html>x
  </xsl:template>

  <xsl:template match="dailyAttendancesReport">
    <table>
      <tr>
	<td class="header"><xsl:value-of select="$Label.Title" /></td>
	<td><xsl:value-of select="title" /></td>
      </tr>
      <tr>
	<td class="header"><xsl:value-of select="$Label.RangeBegin" /></td>
	<td><xsl:value-of select="format-date(periodBegin, '[Y0001]-[M01]-[D01]')" /></td>
      </tr>
      <tr>
	<td class="header"><xsl:value-of select="$Label.RangeEnd" /></td>
	<td><xsl:value-of select="format-date(periodEnd, '[Y0001]-[M01]-[D01]')" /></td>
      </tr>
      <tr>
	<td class="header"><xsl:value-of select="$Label.Generated" /></td>
	<td><xsl:value-of select="format-dateTime(generated, '[Y0001]-[M01]-[D01] [H01]:[m01]:[s01]')" /></td>
      </tr>
    </table>
    <div id="graph"></div>
    <script type="text/javascript">
      (function basic(container) {
      
      var data = [
      <xsl:for-each select="day">
	[<xsl:value-of select="number"/>, <xsl:value-of select="attendances" />],
      </xsl:for-each>
      ]

      var graph = Flotr.draw(container, [data],
        {
	  fontSize: 13,
	  parseFloat: false,
	  bars : {
	    lineWidth: 1,
	    show: true,
	    horizontal: false,
	    barWidth: 1.0
	  },
	  yaxis: {
	    min: 0,
	    autoScale: true
	  },
	  xaxis: {
	    autoscale: true,
	    min: 1,
	    noTicks: <xsl:value-of select="count(day)" />,
	    tickDecimals: 0
          },
	  mouse: {
	    track: true,
	    trackDecimals: 2,
	    relative: true
	  }
      });

      })(document.getElementById("graph"));
    </script>
    <table>
      <tr>
	<td class="header"><xsl:value-of select="$Text.DayNumber" /></td>
	<td class="header"><xsl:value-of select="$Text.Date" /></td>
	<td class="header"><xsl:value-of select="$Text.Attendances" /></td>
      </tr>
      <xsl:apply-templates select="day" />
    </table>
  </xsl:template>
  <xsl:template match="day">
    <tr>
      <td><xsl:value-of select="number" /></td>
      <td><xsl:value-of select="format-date(date, '[Y0001]-[M01]-[D01], [FNn]')" /></td>
      <td><xsl:value-of select="attendances" /></td>
    </tr>
  </xsl:template>
</xsl:stylesheet>
